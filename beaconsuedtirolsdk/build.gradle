apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        snapshot {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.kontaktio:sdk:4.0.3'
    implementation 'io.swagger:swagger-annotations:1.5.22'
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.squareup.okhttp:logging-interceptor:2.7.5'
    implementation 'com.squareup:otto:1.3.8'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.threeten:threetenbp:1.4.0'
    implementation 'io.gsonfire:gson-fire:1.8.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    // Room components
    implementation "androidx.room:room-runtime:2.1.0"
    annotationProcessor "androidx.room:room-compiler:2.1.0"
    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-extensions:2.1.0"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:2.1.0"
    // WorkManager for background tasks
    implementation "androidx.work:work-runtime:2.2.0"
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            groupId 'it.bz.beacon'
            artifactId 'sdk'
            version "${System.getenv('TAG')}"
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/beaconsuedtirolsdk-snapshot.aar")
            pom.withXml {
				final dependenciesNode = asNode().appendNode('dependencies')

				ext.addDependency = { Dependency dep, String scope ->
					if (dep.group == null || dep.version == null || dep.name == null || dep.name == "unspecified")
						return // ignore invalid dependencies

					final dependencyNode = dependenciesNode.appendNode('dependency')
					dependencyNode.appendNode('groupId', dep.group)
					dependencyNode.appendNode('artifactId', dep.name)
					dependencyNode.appendNode('version', dep.version)
					dependencyNode.appendNode('scope', scope)

					if (!dep.transitive) {
						// If this dependency is transitive, we should force exclude all its dependencies them from the POM
						final exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
						exclusionNode.appendNode('groupId', '*')
						exclusionNode.appendNode('artifactId', '*')
					} else if (!dep.properties.excludeRules.empty) {
						// Otherwise add specified exclude rules
						final exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
						dep.properties.excludeRules.each { ExcludeRule rule ->
							exclusionNode.appendNode('groupId', rule.group ?: '*')
							exclusionNode.appendNode('artifactId', rule.module ?: '*')
						}
					}
				}

				// List all "compile" dependencies (for old Gradle)
				configurations.compile.getDependencies().each { dep -> addDependency(dep, "compile") }
				// List all "api" dependencies (for new Gradle) as "compile" dependencies
				configurations.api.getDependencies().each { dep -> addDependency(dep, "compile") }
				// List all "implementation" dependencies (for new Gradle) as "runtime" dependencies
				configurations.implementation.getDependencies().each { dep -> addDependency(dep, "runtime") }
            }
        }
    }
    repositories {
        maven {
            url "s3://it.bz.opendatahub/release"
            credentials(AwsCredentials) {
                accessKey "${System.getenv('S3_REPO_USERNAME')}"
                secretKey "${System.getenv('S3_REPO_PASSWORD')}"
            }
            mavenContent {
                releasesOnly()
            }
        }
        maven {
            url "s3://it.bz.opendatahub/snapshot"
            credentials(AwsCredentials) {
                accessKey "${System.getenv('S3_REPO_USERNAME')}"
                secretKey "${System.getenv('S3_REPO_PASSWORD')}"
            }
            mavenContent {
                snapshotsOnly()
            }
        }
    }
}
